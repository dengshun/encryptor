<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   width="400" height="130" showStatusBar="false"
					   creationComplete="appReadyHandler(event)">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<mx:ProgressBar id="progress" left="10" right="10" top="10" label="PROGRESS: 0%" maximum="100"
					minimum="0" mode="manual" />
	<s:Label y="83" left="10" enabled="false" fontSize="15" text="FAIL:"/>
	<s:Label y="83" left="162" enabled="false" fontSize="15" text="SUCCESS:"/>
	<s:Label id="fail" y="83" left="57" width="71" fontSize="15" text="0"/>
	<s:Label id="success" y="83" left="249" width="140" fontSize="15" text="0"/>
	
	<fx:Script>
		<![CDATA[
			import com.greensock.TweenLite;
			
			import mx.events.FlexEvent;
			
			private var _data:Object;
			private var _wrapper:IWrapper;
			
			[Embed(source="../bin/wrapper.swf", mimeType="application/octet-stream")]
			private var _source:Class;
			
			
			/* listen to user dragging */
			protected function appReadyHandler(e:FlexEvent):void
			{
				var loader:Loader = new Loader();
				loader.contentLoaderInfo.addEventListener(Event.COMPLETE, completeHandler);
				
				var context:LoaderContext = new LoaderContext();
				context.applicationDomain = new ApplicationDomain(ApplicationDomain.currentDomain);
				context.allowCodeImport = true;
				
				var bytes:ByteArray = new _source() as ByteArray;
				loader.loadBytes(bytes, context);
				
				addEventListener(NativeDragEvent.NATIVE_DRAG_ENTER,dragEnterHandler);
				addEventListener(NativeDragEvent.NATIVE_DRAG_DROP, dragDropHandler);		
			}
			
			private function completeHandler(event:Event):void
			{
				// TODO Auto-generated method stub
				var loaderInfo:LoaderInfo = event.currentTarget as LoaderInfo;
				
				_wrapper = loaderInfo.content as IWrapper;
			}

			
			/* 拖进处理 */
			private function dragEnterHandler(event:NativeDragEvent):void
			{
				if (!event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT)) return;
				
				NativeDragManager.acceptDragDrop(this);
			}
			
			/* 放开鼠标处理 */
			private function dragDropHandler(event:NativeDragEvent):void
			{
				if (!_wrapper) return;
				
				var list:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				
				_wrapper.reset();
				
				var reg:RegExp = /swf$/i;
				var stream:FileStream = new FileStream();
				
				var bytes:ByteArray;
				var queue:Vector.<Object> = new Vector.<Object>();
				
				for each(var file:File in list)
				{
					if(reg.test(file.extension))
					{
						stream.open(file,FileMode.READ);
						bytes = new ByteArray();
						
						stream.readBytes(bytes);
						stream.close();
						
						queue.push({file:file, bytes:bytes});
					}
				}
				
				_data = {value: 0};
				
				goto(0, false);
				fail.text = success.text = "0";
				
				var flag:Boolean, i:int;
				var length:uint = queue.length;
				var results:Vector.<Object> = new Vector.<Object>();
				for (i = 0; i < length; i++)
				{
					flag = true;
					
					try
					{
						_wrapper.addFile(queue[i]);
					}
					catch (err:Error)
					{
						flag = false;
					}
					
					goto(100 * 0.4 * (i + 1) / length);
					
					if (flag)
					{
						results.push(queue[i]);
						success.text = results.length.toString();
					}
					else
					{
						fail.text = (i + 1 - results.length).toString()
					}
				}
				
				var settings:XML = _wrapper.encrypt();
				goto(60);
				
				var root:String = File.desktopDirectory.nativePath + "/encrypt/";
				
				stream = new FileStream();
				
				length = results.length;
				if (length == 0) goto(100);
				
				var item:Object;
				for (i = 0; i < length; i++)
				{
					item = results[i];
					stream.open(new File(root + item.file.name), FileMode.WRITE);
					stream.writeBytes(_wrapper.repack(item));
					goto(60 + 100 * 0.4 * (i + 1) / length);
					stream.close();
				}
				
				stream.open(new File(root + "settings.xml"), FileMode.WRITE);
				stream.writeMultiByte(settings.toXMLString(), "utf-8");
				stream.close();
			}
			
			private function goto(percent:Number, tween:Boolean = true):void
			{
				var callback:Function = function():void
				{
					progress.setProgress(_data.value, 100);
					progress.label = "PROGRESS: " + (_data.value >> 0) + "%";
				};
				
				if (tween)
				{
					TweenLite.killTweensOf(_data);
					TweenLite.to(_data, 0.3, {value: percent, onUpdate:callback});
				}
				else
				{
					_data.value = percent;
					callback.call();
				}
			}
			
		]]>
	</fx:Script>
</s:WindowedApplication>